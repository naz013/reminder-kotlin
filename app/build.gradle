apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 29
    flavorDimensions "level"
    defaultConfig {
        applicationId 'com.cray.software.justreminder'
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 281
        versionName '8.1.10'
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        renderscriptTargetApi 21
        renderscriptSupportModeEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildFeatures {
        dataBinding {
            enabled = true
        }
    }
    signingConfigs {
        freeApp {
            storeFile file('justreminder.jks')
            storePassword 'justreminder140419912014'
            keyAlias 'com.cray.software.justreminder'
            keyPassword 'nazar19912014'
        }
        proApp {
            storeFile file('justReminderPro.jks')
            storePassword 'justreminderpro24072014'
            keyAlias 'com.cray.software.justreminderpro'
            keyPassword 'craysoftware2014'
        }
    }
    productFlavors {
        free {
            dimension "level"
            applicationId 'com.cray.software.justreminder'
            buildConfigField "boolean", "IS_PRO", "false"
            buildConfigField "String", "PROJECT_ID", "\"mythical-geode-603\""
        }
        pro {
            dimension "level"
            applicationId 'com.cray.software.justreminderpro'
            buildConfigField "boolean", "IS_PRO", "true"
            buildConfigField "String", "PROJECT_ID", "\"future-graph-651\""
            versionCode 251
        }
    }
    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/proguard/androidx-annotations.pro'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
            zipAlignEnabled true
            jniDebuggable false
            productFlavors.free.signingConfig signingConfigs.freeApp
            productFlavors.pro.signingConfig signingConfigs.proApp
        }
        debug {
            minifyEnabled false
            signingConfig signingConfigs.proApp
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
        exclude module: 'httpclient'
        exclude group: 'com.google.guava', module: 'listenablefuture'
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    dexOptions {
        javaMaxHeapSize "2g"
    }
}

dependencies {
    implementation('com.google.apis:google-api-services-calendar:v3-rev379-1.25.0') {
        exclude group: 'com.google.guava', module: 'listenablefuture'
    }
    implementation('com.google.apis:google-api-services-tasks:v1-rev59-1.25.0')
    implementation 'com.google.http-client:google-http-client-gson:1.33.0'
    implementation('com.google.api-client:google-api-client-android:1.30.5') {
        exclude group: 'org.apache.httpcomponents'
    }
    implementation('com.google.apis:google-api-services-drive:v3-rev173-1.25.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    implementation project(':voiceengine')

    implementation 'org.koin:koin-android:2.0.1'
    implementation "org.koin:koin-androidx-scope:2.0.1"
    implementation "org.koin:koin-androidx-viewmodel:2.0.1"

    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'com.google.android.material:material:1.2.0-alpha04'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta4'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0-alpha03'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    implementation 'androidx.core:core-ktx:1.1.0'
    implementation 'androidx.fragment:fragment-ktx:1.2.0'
    implementation "androidx.biometric:biometric:1.0.1"

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.2'

    implementation('androidx.work:work-runtime:2.3.0') {
        exclude group: 'com.google.guava', module: 'listenablefuture'
    }
    implementation('androidx.work:work-runtime-ktx:2.3.0') {
        exclude group: 'com.google.guava', module: 'listenablefuture'
    }

    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.2.0'
    implementation 'androidx.room:room-runtime:2.2.3'
    implementation 'androidx.room:room-ktx:2.2.3'
    kapt 'androidx.room:room-compiler:2.2.3'

    implementation "androidx.navigation:navigation-fragment-ktx:2.2.0"
    implementation "androidx.navigation:navigation-ui-ktx:2.2.0"

    implementation "androidx.palette:palette-ktx:1.0.0"

    implementation 'com.google.android.gms:play-services-location:17.0.0'
    implementation 'com.google.android.gms:play-services-maps:17.0.0'
    implementation 'com.google.android.gms:play-services-gcm:17.0.0'
    implementation 'com.google.android.gms:play-services-auth:17.0.0'
    implementation('com.google.firebase:firebase-config:19.1.0') {
        exclude group: 'com.google.guava', module: 'guava-jdk5'
        exclude group: 'com.google.guava', module: 'listenablefuture'
    }
    implementation 'com.google.firebase:firebase-core:17.2.2'
    implementation 'com.google.firebase:firebase-messaging:20.1.0'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'

    implementation 'com.google.firebase:firebase-database:19.2.0'
    freeImplementation 'com.google.firebase:firebase-ads:18.3.0'

    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'de.hdodenhof:circleimageview:3.0.1'
    implementation('com.github.bumptech.glide:glide:4.10.0') {
        exclude group: "com.android.support"
    }
    kapt 'com.github.bumptech.glide:compiler:4.10.0'
    implementation 'com.tuyenmonkey:mkloader:1.4.0'
    implementation 'com.evernote:android-job:1.4.2'
    implementation 'com.squareup.retrofit2:retrofit:2.6.2'
    implementation 'com.squareup.retrofit2:converter-gson:2.6.2'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.darwinsys:hirondelle-date4j:1.5.1'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.2.2'
    implementation 'com.dropbox.core:dropbox-core-sdk:3.1.1'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'org.dmfs:lib-recur:0.11.3'
    implementation 'org.apache.commons:commons-lang3:3.9'
    implementation 'com.github.florent37:expansionpanel:1.2.2'
    implementation 'com.github.naz013:ColorSlider:1.0.5'
    implementation 'com.airbnb.android:lottie:3.2.2'
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    testImplementation 'junit:junit:4.12'
    testImplementation 'androidx.test:core:1.2.0'
    testImplementation 'org.mockito:mockito-core:2.23.0'
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"

    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
}

androidExtensions {
    experimental = true
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'kotlin-android-extensions'
